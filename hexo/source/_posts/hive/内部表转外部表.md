---
title: 内外部表转化
categories:
  - Hive
abbrlink: b23fa3b
tags:
---

##### 内外部表区别
目前在大数据领域中，大家大多使用Hive引擎来支撑业务需求并构建企业级数据中台。在Hive中，对于表的类型分为内部表和外部表，通常我们也都会在不同的场景中使用这两种类型的表。

对于这两种类型表的区别简单总结一下：1、内部表又称为托管表，Hive管理其数据、元数据，统计信息；外部表的数据由HDFS管理，并不是只能通过Hive方式命令来操作数据。
2、基于第1条，当内部表被删除时，其数据也会删除；但外部表数据仍然存在。（Hive4.0.0通过配置external.table.purge=true可以在删除表的时候同样删除数据）
3、内部表的数据存储在hive.metastore.warehouse.dir参数配置下的路径。而外部表则对于存储位置没有限制。
4、对于CTAS语法不能应用到外部表上，或分桶表上。
5、对于archive/unarchive/truncate/merge/concatenate只能作用于内部表
6、对于事务/ACID只能作用域内部表
7、查询的结果缓存只能作用于内部表
8、当外部表的结构或者分区发生变化，需要使用msck repair table刷新元数据

##### 场景
当然本篇重点不在于介绍两者区别，而是介绍一种大家可能会遇到的一个小技巧即：两者表类型之间的相互转化。

例如：当你在初期设计表的时候，当表类型设置为内部表，但慢慢的发现该表存储的数据还是比较重要的，如果被人误删了，恢复起来可就麻烦了。因此你想把这个表设计成外部表以此来防止数据丢失。

通常情况下对于这种场景，有以下几种解决方案：
1、重新创建一个外部表，将内部表的数据写入到外部表中。然后删除内部表，再将外部表重命名原来的名字。
2、将内部表下的固定存储位置的文件移动到其他目录下，然后删除该表，并创建外部表，同时指定新的路径。

以上这两种方案是比较常见的，都有一个共同点，那就是需要删表、建表。万一顺序搞错，数据也是有被误删的可能。

##### 解决方案
为了保证万无一失，这里可以通过修改表属性信息即可完成内外表的转换。即
```hql
ALTER  TABLE test_table
SET TBLPROPERTIES('EXTERNAL'='TRUE','external.table.purge'='true')
```
注意：external.table.purge参数的作用在前面已经介绍了，如果要删除外部表的时候也要把数据删掉，那么可以加上该参数。

结果验证
接下来实操验证一下。

##### 建表
```hql
--创建表
CREATE TABLE `test_table`(                                          
`id` string,                                                       
`client_id` string,                                                
`remark` string,                                                   
`create_time` string,                                              
`date_id` string,                                                  
`modify_time` string)                                              
COMMENT '测试'                                                         
PARTITIONED BY (                                                     
`date_str` string) ;

--插入数据
set hive.exec.dynamic.partition.mode=nonstrict;
insert overwrite table test_table partition(date_str)
select 1,111,'test1','dfgd','2021-01-13','dfg','2021-01-13'
union all
select 2,112,'test2','gffdgf','2021-01-13','dfg','2021-01-14'
union all
select 3,113,'test3','gfdg','2021-01-13','dfg','2021-01-15'
```

##### 内部表转外部表
```hql
-- 将内部表转换为外部表
ALTER  TABLE test_table SET TBLPROPERTIES('EXTERNAL'='TRUE');
--删除表
drop table test_table;
```

##### 外部表转内部表
```hql
--前面已经把表删除了，需要重新建表置为外部表
-- 再次将外部表转为内部表
ALTER  TABLE test_table SET TBLPROPERTIES('EXTERNAL'='FALSE');

--需要修复元数据
msck repair table test_table;

--删除表
drop table test_table;
```
